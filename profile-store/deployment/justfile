set dotenv-load

##########
# Set up #
##########

# 1. Initialize a quick context
init-quick:
    quick context create --host "$QUICK_URL" --key "$QUICK_API_KEY" --context "$QUICK_CONTEXT"

# 2. Create a gateway
create-gateway:
   quick gateway create "$GATEWAY" $GATEWAY_ARGS

# 3. Apply the GraphQL-schema to the gateway
apply-schema:
    quick gateway apply "$GATEWAY" -f "$SCHEMA_FILE"

# 4. Create the topics:
create-topics: create-listeningevents-topic create-data-topics create-app-topics

# 5. deploy the apps
deploy-apps: deploy-metrics-apps deploy-charts-apps deploy-recommender-app

# 6. Initialize the record data topics with demo data
init-data: init-artist-data init-album-data init-track-data

# 7. Start a listeningstream by looping on the demo data
produce-listeningevents config="streams-apps/listeningevents-loop.yaml":
    helm upgrade --install \
                 --values streams-apps/base.yaml \
                 --values {{config}} \
                 --set image=$IMAGE_REGISTRY/quick-demo-profile-listenings-producer \
                 --set imageTag="$DEFAULT_IMAGE_VERSION" \
                 --set streams.outputTopic="$LISTENINGEVENTS" \
                 --debug --namespace "$K8S_NAMESPACE" \
                 "$LISTENINGEVENTS" bakdata-common/producer-app

#############
# Tear down #
#############

# if not ended yet, stop emitting listening events
stop-listeningevents:
    helm uninstall "$LISTENINGEVENTS" --namespace "$K8S_NAMESPACE"

# remove finished data producers
rm-data-producers:
    for job in $ARTISTS $ALBUMS $TRACKS; do \
        helm uninstall $job --namespace "$K8S_NAMESPACE"; \
    done;

# delete apps and clear automatically created topics
delete-apps: delete-metrics-apps delete-charts-apps delete-recommender-app

# delete the manually created topics
delete-topics: delete-listeningevents-topic delete-data-topics delete-app-topics

# delete the gateway
delete-gateway:
    quick gateway delete $GATEWAY

############
# Frontend #
############

push-frontend-image: build-image
    docker push $IMAGE_REGISTRY/quick-demo-profile-frontend:$FRONTEND_VERSION

build-image:
    cd .. && docker build . -f ./deployment/frontend/Dockerfile -t $IMAGE_REGISTRY/quick-demo-profile-frontend:$FRONTEND_VERSION

deploy-frontend:
    helm upgrade --install \
                 --values ./frontend/values.yaml \
                 --set app.quickHost="$QUICK_HOST" \
                 --set app.gateway="$GATEWAY" \
                 --namespace "$K8S_NAMESPACE" \
                 $FRONTEND ./charts/profile-store-app

delete-frontend:
    helm uninstall $FRONTEND --namespace $K8S_NAMESPACE


###################
# Grouped Recipes #
###################

# create-topics
create-data-topics: create-artist-topic create-album-topic create-tracks-topic
create-app-topics: create-metrics-topics create-charts-topics
create-metrics-topics: create-counts-topic create-firstlisten-topic create-lastlisten-topic
create-charts-topics: create-topartists-topic create-topalbums-topic create-toptracks-topic

# deploy-apps
deploy-metrics-apps: deploy-counts-app deploy-firstlisten-app deploy-lastlisten-app
deploy-charts-apps: deploy-topartists-app deploy-topalbums-app deploy-toptracks-app

# delete-apps
delete-metrics-apps: delete-count-app delete-firstlisten-app delete-lastlisten-app
delete-charts-apps: delete-topartists-app delete-topalbums-app delete-toptracks-app

# delete-topics
delete-data-topics: delete-artist-topic delete-album-topic delete-track-topic
delete-app-topics: delete-metrics-topics delete-charts-topics
delete-metrics-topics: delete-count-topic delete-firstlisten-topic delete-lastlisten-topic
delete-charts-topics: delete-topartists-topic delete-topalbums-topic delete-toptracks-topic

##################
# Single Recipes #
##################

# create-topics
create-artist-topic:
    quick topic create $ARTISTS --key long --value schema -s ${GATEWAY}.Item
create-album-topic:
    quick topic create $ALBUMS --key long --value schema -s ${GATEWAY}.Item
create-tracks-topic:
    quick topic create $TRACKS --key long --value schema -s ${GATEWAY}.Item
create-listeningevents-topic:
    quick topic create $LISTENINGEVENTS --key long --value schema -s ${GATEWAY}.ListeningEvent
create-counts-topic:
    quick topic create $COUNTS --key long --value long
create-firstlisten-topic:
    quick topic create $FIRST_LISTENINGEVENTS --key long --value long
create-lastlisten-topic:
    quick topic create $LAST_LISTENINGEVENTS --key long --value long
create-topartists-topic:
    quick topic create $TOP_ARTISTS --key long --value schema -s ${GATEWAY}.Charts
create-topalbums-topic:
    quick topic create $TOP_ALBUMS --key long --value schema -s ${GATEWAY}.Charts
create-toptracks-topic:
    quick topic create $TOP_TRACKS --key long --value schema -s ${GATEWAY}.Charts

# init-data
init-single-data id file:
  helm upgrade --install \
               --values streams-apps/base.yaml \
               --set nameOverride={{id}} \
               --set image=$IMAGE_REGISTRY/quick-demo-profile-items-producer \
               --set imageTag=$DEFAULT_IMAGE_VERSION \
               --set streams.outputTopic={{id}} \
               --set streams.productive=$PRODUCTIVE \
               --set commandLine.dataset="app/data/LFM-1b-sample/{{file}}" \
               --debug --namespace $K8S_NAMESPACE {{id}} bakdata-common/producer-app
init-artist-data: (init-single-data "$ARTISTS" "LFM-1b_artists-sample.txt")
init-album-data: (init-single-data "$ALBUMS" "LFM-1b_albums-sample.txt")
init-track-data: (init-single-data "$TRACKS" "LFM-1b_tracks-sample.txt")

# deploy-apps
deploy-counts-app:
    quick app deploy $COUNTS \
                 --registry $IMAGE_REGISTRY --image quick-demo-profile-listenings-count --tag $DEFAULT_IMAGE_VERSION \
                 --args input-topics=$LISTENINGEVENTS \
                        output-topic=$COUNTS \
                        productive=$PRODUCTIVE

deploy-useractivity-app id kind:
    quick app deploy {{id}} \
                 --registry $IMAGE_REGISTRY --image quick-demo-profile-listenings-activity --tag $DEFAULT_IMAGE_VERSION \
                 --args input-topics=$LISTENINGEVENTS \
                        output-topic={{id}} \
                        kind={{kind}} \
                        productive=$PRODUCTIVE

deploy-firstlisten-app: (deploy-useractivity-app "$FIRST_LISTENINGEVENTS" "FIRST")
deploy-lastlisten-app: (deploy-useractivity-app "$LAST_LISTENINGEVENTS" "LAST")

deploy-charts-app id field:
    quick app deploy {{id}} --registry $IMAGE_REGISTRY --image quick-demo-profile-listenings-charts --tag $DEFAULT_IMAGE_VERSION \
                   --args input-topics=$LISTENINGEVENTS \
                          output-topic={{id}} \
                          field={{field}} \
                          productive=$PRODUCTIVE

deploy-topartists-app: (deploy-charts-app "$TOP_ARTISTS" "ARTIST")
deploy-topalbums-app: (deploy-charts-app "$TOP_ALBUMS" "ALBUM")
deploy-toptracks-app: (deploy-charts-app "$TOP_TRACKS" "TRACK")

deploy-recommender-app:
    quick app deploy $RECOMMENDER --registry $IMAGE_REGISTRY --image quick-demo-profile-recommender --tag $DEFAULT_IMAGE_VERSION \
                                  --port=$RECOMMENDER_PORT \
                                  --args input-topics=$LISTENINGEVENTS \
                                         productive=$PRODUCTIVE

# delete apps
delete-count-app:
    quick app delete $COUNTS
delete-firstlisten-app:
    quick app delete $FIRST_LISTENINGEVENTS
delete-lastlisten-app:
    quick app delete $LAST_LISTENINGEVENTS
delete-topartists-app:
    quick app delete $TOP_ARTISTS
delete-topalbums-app:
    quick app delete $TOP_ALBUMS
delete-toptracks-app:
    quick app delete $TOP_TRACKS
delete-recommender-app:
    quick app delete $RECOMMENDER

# delete-topics
delete-artist-topic:
    quick topic delete $ARTISTS
delete-album-topic:
    quick topic delete $ALBUMS
delete-track-topic:
    quick topic delete $TRACKS
delete-listeningevents-topic:
    quick topic delete $LISTENINGEVENTS
delete-count-topic:
    quick topic delete $COUNTS
delete-firstlisten-topic:
    quick topic delete $FIRST_LISTENINGEVENTS
delete-lastlisten-topic:
    quick topic delete $LAST_LISTENINGEVENTS
delete-topartists-topic:
    quick topic delete $TOP_ARTISTS
delete-topalbums-topic:
    quick topic delete $TOP_ALBUMS
delete-toptracks-topic:
    quick topic delete $TOP_TRACKS

###############
# Development #
###############

push-listeningevents: (produce-listeningevents "streams-apps/listeningevents-once.yaml")
